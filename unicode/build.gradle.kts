import me.zolotov.kodepoint.generator.generateUnicodeData
import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

plugins {
    id("kodepoint.multiplatform")
}

kotlin {
    sourceSets {
        commonMain {
            kotlin.srcDir(layout.buildDirectory.dir("generated/sources/unicode-data"))
            dependencies {
                implementation(project(":common"))
            }
        }
        jvmTest {
            dependencies {
                implementation(kotlin("test"))
            }
        }
    }
}

val generateUnicodeData by tasks.registering {
    val outputDir = layout.buildDirectory.dir("generated/sources/unicode-data")
    val cacheDir = layout.buildDirectory.dir("unicode-cache")
    outputs.dir(outputDir)

    doLast {
        generateUnicodeData(
            outputDir.get().asFile.toPath(),
            cacheDir.get().asFile.toPath(),
            "Generated by ./gradlew :unicode:generateUnicodeData"
        )
    }
}

tasks.withType<KotlinCompilationTask<*>>().configureEach {
    dependsOn(generateUnicodeData)
}
