import me.zolotov.kodepoint.generator.generateUnicodeScript
import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

plugins {
    id("kodepoint.multiplatform")
}

kotlin {
    sourceSets {
        commonMain {
            kotlin.srcDir(layout.buildDirectory.dir("generated/sources/unicode-script"))
        }
    }
}

val generateUnicodeScript by tasks.registering {
    val outputDir = layout.buildDirectory.dir("generated/sources/unicode-script")
    val cacheDir = layout.buildDirectory.dir("unicode-cache")
    outputs.dir(outputDir)

    doLast {
        generateUnicodeScript(
            outputDir.get().asFile.toPath(),
            cacheDir.get().asFile.toPath(),
            "Generated by ./gradlew :common:generateUnicodeScript"
        )
    }
}

tasks.withType<KotlinCompilationTask<*>>().configureEach {
    dependsOn(generateUnicodeScript)
}

tasks.matching {
    it.name.endsWith("sourcesjar", ignoreCase = true) || it.name.contains("dokka")
}.configureEach {
    dependsOn(generateUnicodeScript)
}